# LAB 24.10 - Duże obiekty tekstowe (CLOB)

## Zadanie 1
```sql
CREATE TABLE DOKUMENTY (
    ID NUMBER(12) PRIMARY KEY,
    DOKUMENT CLOB
);
```

## Zadanie 2
```sql
DECLARE
    V_TEXT CLOB;
    V_FRAGMENT VARCHAR2(100) := 'Oto tekst. ';
BEGIN
    V_TEXT := EMPTY_CLOB();

    FOR I IN 1..10000 LOOP
        V_TEXT := V_TEXT || V_FRAGMENT;
    END LOOP;
    
    INSERT INTO DOKUMENTY (ID, DOKUMENT)
    VALUES (1, V_TEXT);
    
    COMMIT;
END;
```

## Zadanie 3
```sql
-- a)
SELECT * FROM DOKUMENTY;

-- b)
SELECT ID, UPPER(DOKUMENT) FROM DOKUMENTY;

-- c)
SELECT ID, LENGTH(DOKUMENT) FROM DOKUMENTY;

-- d)
SELECT ID, DBMS_LOB.GETLENGTH(DOKUMENT) FROM DOKUMENTY;

-- e)
SELECT ID, SUBSTR(DOKUMENT, 5, 1000) FROM DOKUMENTY;

-- f)
SELECT ID, DBMS_LOB.SUBSTR(DOKUMENT, 1000, 5) FROM DOKUMENTY;
```

## Zadanie 4
```sql
INSERT INTO DOKUMENTY (ID, DOKUMENT) VALUES (2, EMPTY_CLOB());
```

## Zadanie 5
```sql
INSERT INTO DOKUMENTY (ID, DOKUMENT) VALUES (3, NULL);

COMMIT;
```

## Zadanie 6
```sql
-- a)
SELECT * FROM DOKUMENTY;

-- b)
SELECT ID, UPPER(DOKUMENT) FROM DOKUMENTY;

-- e)
SELECT ID, SUBSTR(DOKUMENT, 5, 1000) FROM DOKUMENTY;
```

> Dla ID = 2, dokument ma wartość pustą.
> Dla ID = 3, dokument oznaczony jest jako NULL.

```sql
-- c)
SELECT ID, LENGTH(DOKUMENT) FROM DOKUMENTY;

-- d)
SELECT ID, DBMS_LOB.GETLENGTH(DOKUMENT) FROM DOKUMENTY;
```

> Dla ID = 2, długość pokazywana jako 0.
> Dla ID = 3, długość pokazywana jako NULL.

```sql
-- f)
SELECT ID, DBMS_LOB.SUBSTR(DOKUMENT, 1000, 5) FROM DOKUMENTY;
```

> Dla ID = 2 oraz ID = 3, wynik funkcji SUBSTR jest wartością NULL.

## Zadanie 7
```sql
DECLARE 
    LOBD CLOB; 
    FILS BFILE := BFILENAME('TPD_DIR', 'dokument.txt'); 
    DOFFSET INTEGER := 1; 
    SOFFSET INTEGER := 1; 
    LANGCTX INTEGER := 0; 
    WARN INTEGER := NULL; 
BEGIN 
    SELECT DOKUMENT INTO LOBD FROM DOKUMENTY 
    WHERE ID = 2 FOR UPDATE; 

    DBMS_LOB.FILEOPEN(FILS, DBMS_LOB.FILE_READONLY); 
    
    DBMS_LOB.LOADCLOBFROMFILE(LOBD, FILS, DBMS_LOB.LOBMAXSIZE, 
        DOFFSET, SOFFSET, 0, LANGCTX, WARN);

    DBMS_LOB.FILECLOSE(FILS); 
    
    COMMIT; 
    DBMS_OUTPUT.PUT_LINE('Status operacji: ' || WARN); 
END;
```

## Zadanie 8
```sql
UPDATE DOKUMENTY
SET DOKUMENT = TO_CLOB(BFILENAME('TPD_DIR', 'dokument.txt'))
WHERE ID = 3;

COMMIT;
```

## Zadanie 9
```sql
SELECT * FROM DOKUMENTY;
```

## Zadanie 10
```sql
SELECT ID, DBMS_LOB.GETLENGTH(DOKUMENT) AS SIZE_CLOB FROM DOKUMENTY;
```

## Zadanie 11
```sql
DROP TABLE DOKUMENTY;
```

## Zadanie 12
```sql
CREATE OR REPLACE PROCEDURE CLOB_CENSOR (
    P_CLOB IN OUT CLOB,
    P_TEXT_TO_CENSOR IN VARCHAR2
) IS
    V_POSITION NUMBER;
    V_SEARCH_FROM NUMBER := 1;
    V_TEXT_LENGTH NUMBER;
    V_DOTS VARCHAR2(32767);
BEGIN
    V_TEXT_LENGTH := LENGTH(P_TEXT_TO_CENSOR);
    V_DOTS := RPAD('.', V_TEXT_LENGTH, '.');
    
    LOOP
        V_POSITION := DBMS_LOB.INSTR(P_CLOB, P_TEXT_TO_CENSOR, V_SEARCH_FROM, 1);
        EXIT WHEN V_POSITION = 0 OR V_POSITION IS NULL;
        
        DBMS_LOB.WRITE(P_CLOB, V_TEXT_LENGTH, V_POSITION, V_DOTS);
        V_SEARCH_FROM := V_POSITION + V_TEXT_LENGTH;
    END LOOP;
END CLOB_CENSOR;
```

## Zadanie 13
```sql
CREATE TABLE BIOGRAPHIES AS SELECT * FROM ZTPD.BIOGRAPHIES;

DECLARE
    V_BIO CLOB;
BEGIN
    SELECT BIO
    INTO V_BIO
    FROM BIOGRAPHIES
    WHERE PERSON = 'Jara Cimrman'
    FOR UPDATE;
    
    CLOB_CENSOR(V_BIO, 'Cimrman');
    
    COMMIT;
END;

SELECT BIO FROM BIOGRAPHIES WHERE PERSON = 'Jara Cimrman';
```

## Zadanie 14
```sql
DROP TABLE BIOGRAPHIES;
```